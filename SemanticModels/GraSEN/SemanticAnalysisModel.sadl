uri "http://grasen.ge.com/SemanticAnalysisModel.sadl" alias sam.
 
import "http://sadl.org/GrFNExtractionModel.sadl".

// classes and properties to analyze core concepts
{FunctionBlock, LoopBlock, NestedBlock} are types of SubGraph.

LHS (note "a var that appears on the LHS of an assignment") is a type of Variable.
{Constant, ExplicitVariable, LiteralInitialized, NotConstant, Structure} are types of Variable.

ArgParamPair is a class
	described by argument with values of type Variable
	described by parameter with values of type Variable.
argumentParameterPair describes HyperEdge with values of type ArgParamPair.

Multiplication is a type of ExpNode
	described by multiplicand with values of type ExpNode.
Subtraction is a type of ExpNode
	described by minuend with values of type ExpNode
	described by subtrahend with values of type ExpNode.
Summation is a type of ExpNode
	described by summand with values of type ExpNode.

inBlock describes Variable with values of type SubGraph.
sameStruct describes Variable with values of type Variable.
sameVar describes Variable with values of type Variable.
varName describes Variable with values of type string.

// domain specific concepts related to core concepts
{ErrorSignal, ErrorSignalCandidate} are types of Variable
	described by referenceSignal with values of type ReferenceSignal
	described by measurementSignal with values of type MeasurementSignal
    described by referenceSignalCandidate with values of type ReferenceSignal
	described by measurementSignalCandidate with values of type MeasurementSignal.
{MeasurementSignal, MeasurementSignalCandidate} are types of Variable.

errorVariable describes ExpNode with values of type ErrorSignal.
proportionalConstant describes ExpNode with values of type Constant.
derivativeConstant describes ExpNode with values of type Constant.
integratorVariable describes ExpNode with values of type Integrator.
integratorConstant describes ExpNode with values of type Constant.
derivativeVariable describes ExpNode with values of type Derivative.
derivativeConstant describes ExpNode with values of type Constant.

{Integrator, IntegratorTypo} are types of Variable. 
{PrevError, PrevErrorCandidate} are types of Variable.
{DerivativeCandidate, Derivative} are types of Variable
	described by error with values of type ErrorSignal
	described by prevError with values of type PrevError
    described by errorCandidate with values of type ErrorSignalCandidate 
	described by prevErrorCandidate with values of type PrevErrorCandidate.
{PropExpNode, IntExpNode, DervExpNode} are types of ExpNode. // structure corresponding to Kp_M * (error - error_last) etc.
//{P-Controller, PI-Controller, PD-Controller, PID-Controller} are types of Node 

semanticExpression describes Variable with values of type string.	

// classes and properties we will use to print out results in the csv file
ControllerConstruct is a class // this will be what we write out to csv file
	described by constructName with values of type string
	described by linebegin with values of type int
	described by lineend with values of type int. 
	
Constant is a type of ControllerConstruct.
{DerivativeConstant, IntegratorConstant, ProportionalConstant, ReferenceSignal, ReferenceSignalCandidate} are types of Constant.
{DerivativeConstant, IntegratorConstant, ProportionalConstant} are types of ControllerConstruct.

{P-ControllerCandidate, PI-ControllerCandidate, PD-ControllerCandidate} are types of Node.

Controller is a class.
{P-Controller, PI-Controller, PD-Controller, PID-Controller} are types of Controller.

{Derivative, ErrorSignal, Integrator, IntegratorTypo, MeasurementSignal, PrevError, ReferenceSignal,
 P-Controller, PI-Controller, PD-Controller, PID-Controller} are types of ControllerConstruct.

description of Constant             is "This is a constant.".
description of Derivative           is "This is an Derivative.".
description of DerivativeConstant   is "This is the constant multiplier for the Derivative.".
description of ErrorSignal          is "This is an error signal.".
description of Integrator           is "This is an Integrator.".
description of IntegratorConstant   is "This is the constant multiplier for the Integrator.".
description of IntegratorTypo       is "This is close to being an Integrator - is there a typo?.".
description of MeasurementSignal    is "This is a measurement signal.".
description of PrevError            is "This is previous error.".
description of ProportionalConstant is "This is the constant multiplier for the Proportional term.".
description of ReferenceSignal      is "This is a reference signal.".
description of P-Controller         is "This is a P Controller.".
description of PI-Controller        is "This is a PI Controller.".
description of PD-Controller        is "This is a PD Controller.".
description of PID-Controller       is "This is a PID Controller.".